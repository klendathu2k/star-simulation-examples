MACRO runAuAuSample nevents=1 run=11022 set=1 TAG=y2007h

   LIBRARY =                                        $STAR_LIB
   BINARY  =                                        $STAR_BIN
   FIELD   =                                             -5.0


   PTmin   =                                            0.000
   PTmax   =                                          100.000
   ETAmin  =                                          -3.0
   ETAmax  =                                          +3.0
   Zmin    =                                         -200.000
   Zmax    =                                         +200.000

   Luxury  =                                                3
   Seed    =                                      [run]//[set]

   XVERTEX  =                                     0.000
   YVERTEX  =                                     0.000
   ZVERTEX  =                                     0.000

   XSIGMA   =                                     0.010
   YSIGMA   =                                     0.010
   ZSIGMA   =                                    60.000

**
** Build the geometry and load shared libraries
**
   MESSAGE ''
   MESSAGE '================================================================================='
   MESSAGE '                                                             ' $TIME $DATE
   MESSAGE Geometry: [tag]
   MESSAGE Library:  [library]
   MESSAGE Field:    [field] 
   MESSAGE ''
   MESSAGE Ranlux configuration:
   MESSAGE   seed   = [seed]
   MESSAGE   luxury = [luxury]
   MESSAGE Vertex distribution:
   MESSAGE   x = [xvertex] +/- [xsigma]
   MESSAGE   y = [yvertex] +/- [ysigma]
   MESSAGE   z = [zvertex] +/- [zsigma]

   EXEC SetupHijing [run] [nevents]

   MESSAGE ''
   MESSAGE '=================================================================================='
   MESSAGE ''


   DETP geom [tag] field=[field]
   gexec $STAR_LIB/geometry.so
   gexec $STAR_LIB/gstar.so
   gclos all

   swit 1 2        !the content of the temporary stack for secondaries in the common /GCKING/ is printed;

**
** Setup random number seed and switch random number generators
** to ranlux with luxury level 3.  Will ensure independent seeds
** for up to 9999 different sets in the run
**
   RANLUX [seed] [luxury]

**
** Open the output file
**
   FILE     = rcf[RUN]_[SET]_[NEVENTS]evts.fzd
   gfile o [FILE]

   gvertex [XVERTEX] [YVERTEX] [ZVERTEX]
   gspread [XSIGMA]  [YSIGMA]  [ZSIGMA]

*  AGUSER/GKINE NTRACK ID [ PTLOW PTHIGH YLOW YHIGH PHILOW PHIHIGH ZLOW ZHIGH option ]
   aguser/gkine  -2    0    0.    650.0  [ETAmin] [ETAmax] 0. 6.29 [Zmin] [Zmax]
   USER/INPUT please evgen.1.nt

**
** Perform the event loop
**
   DO i=0,[nevents]
      Message Event [i] of [nevents]
      trig 1
   ENDDO

   EXIT

RETURN  

MACRO SetupHijing run 

   IF [run]=11022 THEN
      SHELL $STAR_BIN/hijing_382 -inp hijev.inp
   ENDIF
   IF [run]=11023 THEN
      SHELL $STAR_BIN/hijing_382 -inp hijev15.inp
   ENDIF
   IF [run]=11024 THEN
      SHELL $STAR_BIN/hijing_382 -inp hijev11GeV.inp
   ENDIF 


RETURN