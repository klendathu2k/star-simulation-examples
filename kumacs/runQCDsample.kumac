**************************************************************************************************** 
** MACRO runQCDsample
** 
** tag.pp200.pythia6_4_22.tune100.y2005h.gheisha_on 
MACRO runQCDsample NEVENTS=10 RUN=10020 SET=1000 WHERE=cloud roots=200

                                                              ** run configuration
   LIBRARY  =                                 $STAR_LIB 
   TAG      =                                    y2005h
                                                              ** pythia configuration
   PYTHIA   =                        libPythia6_4_22.so
   ETAmin   =                                     -10.0 
   ETAmax   =                                     +10.0 

                                                               ** vertex in cm
   XVERTEX  =                                     0.000 
   YVERTEX  =                                     0.000
   ZVERTEX  =                                     0.000

   XSIGMA   =                                     0.010 
   YSIGMA   =                                     0.010
   ZSIGMA   =                                    60.000
                                                                ** open output file
   FILE     = [WHERE]//[RUN]_[SET]_[NEVENTS]evts.fzd
   gfile o [FILE]


                                                                ** setup random number generator
                                                                ** run number, and starting event
   rndm [RUN] [SET]
   istart=[SET]-1000
   rung [RUN] [istart]
                                                                ** setup geometry 
   detp    geom [TAG] 
   gexec   [LIBRARY]/geometry.so
   gexec   [LIBRARY]/gstar.so
   gclose  all

                                                                ** configure pythia
   exec select_pythia [PYTHIA] [LIBRARY]                        ** select pythia version 
   TFM = "http://projects.hepforge.org/pythia6/"                ** RTFM for more information

                                                  
                                                                ** setup the collision kinematics for pythia
   frame CMS
   beam  p p
   ener  [ROOTS]                          
                                                                ** set the pythia tune
   MSEL 1            !- Total cross section, i.e. elastic+inelastic D=1
   TUNE 100          !- Pythia tune (CDF tune A)

   gvertex   [xvertex] [yvertex] [zvertex]
   gspread   [xsigma]  [ysigma]  [zsigma]

                                                                ** setup histograms w/ title
   title [file] [tag] [run] [set] [pythia]

                                                                ** setup the requested interaction
   exec  SELECT_PT [run]
                                                                ** trigger and list one event
   trig 1 
   call pylist(1)                                

   DO i=2,[nevents]
                                                                ** run the rest of the simulation
     TRIG 1

   ENDDO

                                                                 ** this is the end of starsim
   EXIT
   
Return

****************************************************************************************************
** MACRO select_pythia
**
** Selects the pythia library and loads the interactive pythia command definitions.  Additionally,
** it flags several decay modes as stable so that geant will handle them.
**
MACRO select_pythia pythia=libpythia_6410t.so path=$STAR_LIB

** message Loading [path]/[pythia]
   GExec [path]/[pythia]
** message Loading [path]/bpythia.so
   GExec [path]/bpythia.so

   ****************************************************************
   ** These particles will be decayed by geant instead of pythia **
   MDCY (102,1)=0  ! PI0 111
   MDCY (106,1)=0  ! PI+ 211
   MDCY (109,1)=0  ! ETA 221
   MDCY (116,1)=0  ! K+ 321
   MDCY (112,1)=0  ! K_SHORT 310
   MDCY (105,1)=0  ! K_LONG 130
   MDCY (164,1)=0  ! LAMBDA0 3122
   MDCY (167,1)=0  ! SIGMA0 3212
   MDCY (162,1)=0  ! SIGMA- 3112
   MDCY (169,1)=0  ! SIGMA+ 3222
   MDCY (172,1)=0  ! Xi- 3312
   MDCY (174,1)=0  ! Xi0 3322
   MDCY (176,1)=0  ! OMEGA- 3334
   ****************************************************************

Return

MACRO QCD PTmin PTmax
   CKIN (3)=[PTmin]  !-
   CKIN (4)=[PTmax]  !- 
RETURN

MACRO SELECT_PT run

   IF ( [run] .eq. 10020 ) THEN ; EXEC QCD 2.0 3.0   ; ENDIF
   IF ( [run] .eq. 10021 ) THEN ; EXEC QCD 3.0 4.0   ; ENDIF
   IF ( [run] .eq. 10022 ) THEN ; EXEC QCD 4.0 5.0   ; ENDIF
   IF ( [run] .eq. 10023 ) THEN ; EXEC QCD 5.0 7.0   ; ENDIF
   IF ( [run] .eq. 10024 ) THEN ; EXEC QCD 7.0 9.0   ; ENDIF
   IF ( [run] .eq. 10025 ) THEN ; EXEC QCD 9.0 11.0  ; ENDIF
   IF ( [run] .eq. 10026 ) THEN ; EXEC QCD 11.0 15.0 ; ENDIF
   IF ( [run] .eq. 10027 ) THEN ; EXEC QCD 15.0 25.0 ; ENDIF
   IF ( [run] .eq. 10028 ) THEN ; EXEC QCD 25.0 35.0 ; ENDIF
   IF ( [run] .eq. 10029 ) THEN ; EXEC QCD 35.0 -1.0 ; ENDIF

RETURN
